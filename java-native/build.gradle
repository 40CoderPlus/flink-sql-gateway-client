import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

apply plugin: 'org.openapi.generator'

dependencies {
    implementation 'io.swagger:swagger-annotations:1.6.8'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

openApiGenerate {
    generatorName = 'java'
    library = 'native'
    inputSpec = "$rootDir/spec/rest_v2_sql_gateway.yml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = 'com.fortycoderplus.flink.ext.sqlgateway.api'
    invokerPackage = 'com.fortycoderplus.flink.ext.sqlgateway.invoker'
    modelPackage = 'com.fortycoderplus.flink.ext.sqlgateway.model'
    configOptions = [
            dateLibrary: 'java8'
    ]
}

task buildV1SDK(type: GenerateTask) {
    generatorName = 'java'
    library = 'native'
    inputSpec = "$rootDir/spec/rest_v1_sql_gateway.yml".toString()
    outputDir = "$buildDir/generatedv1".toString()
    apiPackage = 'com.fortycoderplus.flink.ext.sqlgateway.apiv1'
    invokerPackage = 'com.fortycoderplus.flink.ext.sqlgateway.invokerv1'
    modelPackage = 'com.fortycoderplus.flink.ext.sqlgateway.modelv1'
    configOptions = [
            dateLibrary: 'java8'
    ]
}

sourceSets.main.java.srcDirs += ["$buildDir/generated/src/main/java", "$buildDir/generatedv1/src/main/java"]